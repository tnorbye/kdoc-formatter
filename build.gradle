buildscript {
    apply from: "$rootDir/version.gradle"

    ext {
        gradlePluginVersion = '7.4.0-alpha08'
    }

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    //id 'com.ncorti.ktfmt.gradle' version '0.8.0'
}

group 'kdocformatter'
version '1.0'

repositories {
    google()
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.7.0"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.46.0"
}

task format(type: JavaExec, group: "verification") {
    description = "Format lint Kotlin files"
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "--disabled_rules=import-ordering", "**/*.kt"
}

//ktfmt {
//    kotlinLangStyle()
//}

task lint {
    dependsOn ':library:lint', ':cli:lint', ':ide-plugin:lint'
}

task install {
    dependsOn ':cli:installDist', 'test'
}

task zip {
    dependsOn ':cli:distZip', 'test'
}

task runIde {
    dependsOn ':ide-plugin:runIde'
}

task plugin {
    dependsOn ':ide-plugin:buildPlugin'
}

task all {
    dependsOn 'clean', 'install', 'plugin', 'zip'
}

clean.doFirst {
    delete "${rootDir}/m2"
    delete "${rootDir}/gradle-plugin/build"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}
